title: Otto Braun
is-pinned-card: true
tags: advisor, reformist
card-image: img/portraits/BraunOtto.jpg
view-if: braun_advisor = 1
new-page: true 

= Otto Braun

Braun [? if spd_prussia = 1 : is ?][? if spd_prussia = 0 : was ?] the Minister-President of Prussia, the largest state in Germany. He is a reformist leader who seeks to work with the bourgeois parties.

- @coalition
- @prussian_bulwark
- @snap_election
- @cancellor_appointment
- @root: Return to main

@coalition
title: Negotiating with the Coalition
subtitle: Braun can improve our relationships with our centrist and center-right coalition partners.
choose-if: advisor_action_timer <= 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 5; coalition_dissent -= 1 if spd_in_government = 1; z_relation += 5; ddp_relation += 5; dvp_relation += 5

We have improved our relations with the Z, DDP, and DVP, and reduced tensions with the coalition.

@prussian_bulwark
title: The Prussian Bulwark
subtitle: As the Minister-President of Prussia, Braun has power over the Prussian bureaucracy and security services.
choose-if: advisor_action_timer <= 0 and spd_prussia = 1
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if spd_prussia == 0 : The SPD does not control Prussia. ?]
on-arrival: advisor_action_timer = 5; prussian_affairs_timer = 0
go-to: prussian_affairs

@snap_election
title: Snap Election
subtitle: Call a snap election.
unavailable-subtitle: [? if chancellor != "Braun" and president != "Braun" : Braun must be Chancellor or President. ?][? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer == 0 and (chancellor == "Braun" or president == "Braun")
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; 

We have called for new elections, set for [+ next_election_month : month +] [+ next_election_year +].

@cancellor_appointment
title: Переназначить канцлера
subtitle: Поскольку Браун является президентом при чрезвычайном правительстве, он может в любой момент заменить канцлера.
unavailable-subtitle: [? if president != "Braun" : Braun must be President. ?][? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; 

- @thalmann_chancellor: Appoint Thälmann as chancellor.
- @braun_emergency_chancellor: Otto Braun for Chancellor-President!
- @breitscheid_emergency_chancellor: Rudolf Breitscheid for emergency Chancellor.
- @bruning_emergency_chancellor: Brüning of the Center Party, as a goodwill gesture toward the bourgeoisie.
- @schleicher_emergency_chancellor: Генерал Шлейхер поможет нам поладить с армией.

@thalmann_chancellor
choose-if: president == "Braun" and reformist_strength + neorevisionist_strength < left_strength
unavailable-subtitle: [? if kpd_relation <50 : Тельман не согласится стать канцлером ?] [? if reformist_strength >= left_strength : Most in the parliamentary party would not support Thälmann as chancellor. ?]
on-arrival: chancellor = "Thälmann"; chancellor_party = "KPD"; kpd_relation = 80; in_left_front = 1; spd_in_government = 1 

With great reluctance, President [+ president +] appoints [+ chancellor +] as chancellor.

Immediately, [+ chancellor +] goes to work building a "Soviet Germany": consolidating as much power as possible in the KPD, purging state institutions of anti-communists, and sending street fighters against the "enemies of the people".

In response to [+ chancellor +] being appointed Chancellor, the right-wing forces launches almost immediately launch an attempted coup. Troops of the Stahlhelm and Nazi SA enter Berlin and immediately begin clashing with our supporters! Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop the KPD from taking power. We are forced to be on the same side as the KPD, whether we want to or not.


@braun_emergency_chancellor
subtitle: This is unprecedented.
view-if: chancellor != "Braun"
on-arrival: chancellor = "Braun"; chancellor_party = "SPD"; spd_in_government = 1; pro_republic -= 5; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_loyalty -= 0.1; 

Our experts have checked the constitution, and have discovered that there is no specific prohibition against the Chancellor also being the President (the President may not be a member of the Reichstag; there is no law saying that the Chancellor *must* be a member of the Reichstag).

Thus, we have Chancellor-President Otto Braun. He claims that he will resign as soon as the economic-political crisis is over. The right-wing parties, who were once clamoring to replace parliamentary rule with dictatorship or monarchy, are now clamoring for the restoration of democracy.


@bruning_emergency_chancellor
view-if: chancellor != "Brüning"
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"; z_relation += 10;

The Center Party is pleased, and somewhat surprised, that we have appointed [+ chancellor +] as chancellor.


@breitscheid_emergency_chancellor
view-if: chancellor != "Breitscheid"
subtitle: The Communists are warming to him...
on-arrival: chancellor = "Breitscheid"; chancellor_party = "SPD"; kpd_relation += 5;

We have appointed Rudolf Breitscheid as emergency chancellor. The Communists consider him less objectionable than the alternatives, although it is unclear whether they will tolerate this government.


@schleicher_emergency_chancellor
view-if: chancellor != "Schleicher"
on-arrival: chancellor = "Schleicher"; chancellor_party = "I"; pro_republic -= 10; reichswehr_loyalty += 0.3; kpd_relation -= 10; z_relation -= 5; dvp_relation -= 5; ddp_relation -= 5; coup_progress -=1;

[+ president +] appoints Kurt von Schleicher, a former general, as chancellor. Армия приветствует это назначение.

Schleicher's government is deeply unpopular among all parties of the Reichstag.

