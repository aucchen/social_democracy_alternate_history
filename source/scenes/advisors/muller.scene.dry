title: Hermann Müller
is-pinned-card: true 
tags: advisor, centrist
card-image: img/portraits/MüllerHermann.jpg
new-page: true
view-if: muller_advisor = 1

= Herman Müller


Müller is a former [? if chancellor = "Müller": and current ?] Chancellor, and is a long-time Social Democratic leader.

- @coalition
- @toleration
- @snap_election
- @root: Return to main

@coalition
title: Negotiating with the Coalition
subtitle: Müller can improve our relationships with our coalition partners.
choose-if: advisor_action_timer <= 0 and spd_in_government = 1
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_in_government: The SPD is not in government. ?]
on-arrival: advisor_action_timer = 6; coalition_dissent -= 1 if spd_in_government = 1 and coalition_dissent > 0; z_relation += 5; ddp_relation += 5; dvp_relation += 5

We have improved our relations with the Z, DDP, and DVP, and reduced tensions with the coalition.


@toleration
title: Defending Toleration
subtitle: Müller supports the toleration arrangement with the bourgeois parties.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_toleration: The SPD is not in toleration. ?]
choose-if: spd_in_government = 0 and spd_toleration = 1 and advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; left_dissent -= 8; neorevisionist_dissent -= 8; center_dissent -= 5;

We have reduced dissent among the anti-toleration factions. 

@snap_election
title: Snap Election
subtitle: Call a snap election.
unavailable-subtitle: [? if chancellor != "Müller" : Müller must be Chancellor. ?][? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer == 0 and chancellor == "Müller"
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12;

We have called for new elections, set for [+ next_election_month : month +] [+ next_election_year +].
