title: Советы рабочих депутатов берут власть в Пруссии
subtitle: The Chancellor attempts to overthrow the Prussian government.
view-if: (chancellor = "Thälmann" and (kpd_r*1.1) > spd_r and spd_prussia = 1) or (chancellor = "Thälmann" and year >= 1932 and month >= 8 and spd_prussia = 0) or (chancellor = "Thälmann" and kpd_relation < 50 and spd_prussia = 1)
on-arrival: prussian_coup_seen = 1
new-page: true
tags: event
max-visits: 1

= Coup in Prussia

Канцлер [+ chancellor +] распустил прусский ландтаг. Единственным органом власти остались лишь советы рабочих депутатов, где широко представлены коммунисты.

Законность этих мероприятий под большим вопросом. Если мы не вмешаемся, эта земля уйдет под власть комммунистов. Против коммунистов немеделенно выступили правые силы.

- @defend
- @surrender
- @protest
- @protocivil_war
- @nenashedelo

@defend
title: Выступить в поддержку коммунистов
subtitle: Labor will call a general strike. The Reichsbanner and the loyal elements of the Prussian police will resist the coup.
unavailable-subtitle: We do not have the forces to resist.
choose-if: ((rb_strength*rb_militancy) + (rfb_strength*rfb_militancy)) >= 200

Угроза справа для нас куда опаснее и мы вынуждены поддержать коммунистов.

The Reichswehr and Stahlhelm are already surrounding the left-leaning cities, and the SA is supporting them. This will be civil war. We do realize that, correct?

- @defend_2: Civil war it is.
- @surrender

@defend_2
go-to: civil_war
on-arrival: resist_coup = 1


@surrender
title: Не вмешиваться
view-if: spd_prussia = 1
on-arrival: spd_prussia = 0; workers_spd -= 3; center_dissent += 10; neorevisionist_dissent += 10; rb_militancy = rb_militancy/1.4; rb_strength -= 400; rfb_strength += 100; rfb_militancy += 0.35; sa_strength += 30; sh_strength += 10; old_middle_nsdap += 5; kpd_relation += 10;

Коммунисты практически без боя одержали верх. Наша пассивновть вызывает недоумение у наших сторонников.


@protest
title: Опротестовать действия комммунистов как незаконные.
view-if: spd_prussia = 1
choose-if: (prussian_police_strength*prussian_police_loyalty*1.2) > (rfb_strength*rfb_militancy) and judical_reform >= 2
on-arrival: spd_prussia = 1; kpd_relation -= 50; workers_spd += 3; left_coalition = 0 if left_coalition > 0; pro_republic += 10; pacifism += 10; 

Коммунисты, опасаясь совместных действий правых и социал-демократов, пошли на попятную. Советы объявили о самороспуске, законность восстановлен.

@protocivil_war
title: Разгромить Советы.
view-if: spd_prussia = 1
on-arrival: spd_prussia = 1; kpd_relation -= 100; left_coalition = 0 if left_coalition > 0; rfb_banned = 1; rfb_strength = 10; rfb_militancy = rfb_militancy/2; nationalism += 5;  next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; chancellor = 'Schleicher'; nsdap_relation += 5; dnvp_relation += 5; workers_nsdap += 4; workers_dnvp += 2; workers_z += 1; workers_kpd -= 5; workers_spd -= 4; workers_dvp += 1; sa_strength += 50; sh_strength += 20; rb_strength -= 50; z_relation += 5; dvp_relation += 5;

Мы сохранили в своих руках Пруссию, но о союзе с коммунистами больше не может идти и речи. Однако главную роль в подавлении Советов сыграли правые силы - и отныне нам надо с этим считаться. 

Коммунисты оттеснены в подполье, а канцлер Тельман ушел отправлен в отставку. Германия спасена от большевистского переворота.

Новые выборы назначены на [+ next_election_month : month +] [+ next_election_year +].

@nenashedelo
title: Не наше дело.
view-if: spd_prussia = 0
on-arrival: rfb_strength -=40; sa_strength -= 20; sh_strength -= 30; old_middle_nsdap += 5; rfb_militancy += 0.25; sa_militancy += 0.05; workers_spd -= 4; workers_kpd += 5; prussian_government = "KPD"; sh_militancy += 0.05; kpd_relation -= 5; pro_republic -= 5; coup_progress += 1; z_relation -= 5; ddp_relation -= 5; nsdap_relation -= 10; dnvp_relation -= 5; dvp_relation -= 5; workers_nsdap -= 2;

Коммунисты, в стычках с правыми отстояли власть Советов. Обе стороны понесли потери, но приобрели ценный боевой опыт.  

Мы, оставаясь в стороне, навлекли гнев как правых, так и левых. Советизация Германии продолжается.