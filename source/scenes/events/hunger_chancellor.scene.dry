title: The Hunger Chancellor
subtitle: Popular discontent with Chancellor [+ chancellor +] rises.
new-page: true
view-if: year >= 1931 and month >= 2 and unemployed >= 18 and welfare < 0 and works_program == 0
on-arrival: workers_spd *= 0.9 if chancellor_party == "SPD"; workers_spd *= 0.9 if spd_in_government == 1; workers_nsdap += 5 if spd_in_government; workers_kpd += 5 if spd_in_government; unemployed_nsdap += 5 if spd_in_government
tags: event
max-visits: 1

People are calling Chancellor [+ chancellor +] the Hunger Chancellor because of his perceived callousness in the face of the depression. [? if chancellor_party == "SPD" : This is obviously a very bad sign for us. ?]

[? if spd_toleration == 1 : As we are currently tolerating the [+ chancellor +] government, this could very well reflect badly on us. ?]

- @maintain_toleration: Maintain the current governing arrangement.
- @break_toleration: Break toleration; vote against this government.

@maintain_toleration
subtitle: It is necessary to preserve democracy.
view-if: spd_toleration == 1
on-arrival: workers_nsdap += 5; workers_kpd += 5; unemployed_nsdap += 5; workers_spd -= 5; pro_republic -= 5; kpd_relation -= 5

Our policy of toleration continues.


@break_toleration
subtitle: This is likely to result in new elections.
view-if: spd_toleration == 1
on-arrival: next_election_time = time+3 if next_election_time >= time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0;

We have definitively broken with the [+ chancellor +] government, and have voted against them in a vote of no confidence.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
