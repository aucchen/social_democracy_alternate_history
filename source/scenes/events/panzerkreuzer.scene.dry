title: Panzerkreuzer A
subtitle: A political question
tags: event, govt_affairs
is-card: true
on-arrival: panzerkreuzer_seen = 1
view-if: panzerkreuzer_seen = 0 and year = 1928 and month >= 8 and reichswehr_minister_party != "SPD" and spd_in_government = 1

= Panzerkreuzer

The Reichswehr Ministry is demanding the construction of a new battlecruiser, as part of Germany's rearmament. Our coalition partners are mostly united in support, apart from anti-war members of the LVP choosing to abstain. While many of our members are vehemently against new military spending, this vote may be necessary to preserve the coalition. 
We campaigned on food for children instead of battlecruisers. Will we really betray our campaign promises?

- @vote_yes: Vote yes, for constructing the battlecruiser.
- @vote_no: Vote no, against constructing the battlecruiser.
- @abstain: Abstain from the vote.

@vote_yes
on-arrival: left_dissent += 10; labor_dissent += 5; center_dissent += 5; reformist_dissent += 5; neorevisionist_dissent += 5; panzerkreuzer_funded = 1; kpd_coalition_dissent += 1; kpd_relation -= 6; workers_spd -= 3

Our grassroots party members are shocked by our intransigence! Many party branches have begun agitating against the cabinet, and our Reichstag members even plan a full vote on the proposal, contravening the government's decision.

The campaign against the battlecruiser fails, with only the dissident SPD and KPD members voting against. The ship will be built.

@vote_no

We would lack the majority in the Reichstag to successfully block the construction of the Panzerkreuzer, with only the SPD and KPD voting against. We would have to explore other options if we want to prevent the ship from being built.

- @lvp_abstain: Is there any room for political maneuvering?
- @vote_no_chancellor: Can our chancellor convince the cabinet?

@vote_no_chancellor
go-to: braun if chancellor = "Braun"; muller if chancellor = "Müller"; vote_no_pass if chancellor_party != "SPD";

@braun
on-arrival: panzerkreuzer_funded = 0; nationalism -= 3*(1-dissent); panzerkreuzer_failed = 1; workers_spd += 5*(1-dissent);

Our coalition partners are shocked by our intransigence! However, Chancellor Braun manages to persuade our cabinet ministers to drop the plan to vote on the battlecruiser. The ship will not be built!

@muller
on-arrival: panzerkreuzer_funded = 1

Our coalition partners are shocked by our intransigence! Despite Chancellor Müller's rejection of the proposal, the other parties bring the matter to a Reichstag vote, which easily passes, with only the SPD and KPD voting against. The ship will be built.

@vote_no_pass
on-arrival: panzerkreuzer_funded = 1

Our coalition partners are shocked by our intransigence! Chancellor [+ chancellor +] brings the matter to a Reichstag vote, which passes with all parties in favor except the SPD and KPD. The ship will be built.

@vote_fails
on-arrival: panzerkreuzer_funded = 0; coalition_dissent += 1; reichswehr_loyalty -= 0.05; left_dissent -= 10; labor_dissent -= 10; center_dissent -= 5; reformist_dissent -= 5; neorevisionist_dissent -= 5; nationalism -= 5; pacifism += 1; z_relation -= 5; workers_spd += 5*(1-dissent); panzerkreuzer_failed = 1

Military leaders are upset at the Reichstag's decision to not construct the battlecruiser, and they have taken the complaints to our coalition partners. However, there is not much they can do to force the construction, as the anti-war coalition maintains a majority over the Reichstag.

No matter what, the battlecruiser will not be built. We have fulfilled one of our campaign promises, and our supporters are pleased.

@lvp_abstain
choose-if: lvp_relation >= 60 and progressive_coalition >= 50
unavailable-subtitle: [? if lvp_relation < 60 and progressive_coalition >= 50 : We lack the relations with the LVP.  ?] [? if progressive_coalition < 50 : We lack the majority to block the vote. ?]

Our good relations and connections with the anti-war portions of the LVP has bought us the option of blocking the ship's construction with the support of the "progressive coalition," consisting of the KPD, SPD & portions of the LVP. However, the LVP demands us to lend them some resources for them to break their party line.

- @lvp_pay: Convince the liberals.
- @vote_no_chancellor: The final vote...

@lvp_pay
choose-if: resources >= 2
subtitle: -2 resources
unavailable-subtitle: Not enough resources (need 2)
on-arrival: resources -= 2

With our resources, the left-LVPers are convinced to support us on this vote.

- @vote_fails: The final vote...

@abstain
on-arrival: lvp_relation -= 1; z_relation -= 3; left_dissent += 5; labor_dissent += 5; panzerkreuzer_funded = 1; kpd_relation -= 3

Our coalition partners are shocked by our intransigence! Despite our abstention, the vote in the Reichstag goes through, with all parties in favor except the SPD and KPD. The ship will be built.
