title: London Economic Conference
subtitle: The League of Nations unites to tackle the Great Depression...
view-if: year = 1933 and month >= 6
on-arrival: london_economic_conference_seen = 1
new-page: true
max-visits: 1
go-to: economic_recovery if unemployed < 15; no_recovery if unemployed >= 15
tags: event

= London Economic Conference

# TODO: if you've recovered, you get economic recovery
# if you haven't recovered, you can either support Roosevelt or support the UK/France

The League of Nations is hosting a conference to discuss the state of the global economy and potential solutions to the ongoing depression. All parties want to reduce tariffs to encourage trade. However, the Western powers are at an impasse; France and to a lesser extent Britain are calling for fixed currency exchange rates, while President Roosevelt of the United States wants to print new money to stimulate demand, calling for "controlled inflation". The Europeans want to reduce their debts to the US, while America favors continued full payments.

@economic_recovery

[? if wtb_adopted or budget <= 0: Germany has largely recovered from the crisis, owing in large part to the same sorts of economic policies that Roosevelt is pursuing. ?]
[? if not wtb_adopted and budget > 0: Germany has largely recovered from the crisis, without having to resort to budget deficits or devaling the currency. ?]

- @support_roosevelt
- @support_france
- @stable_currency_out_of_gov
- @salvage
- @no_business

@no_recovery

Germany, like the rest of the world, is still deep in the economic crisis, even if it has appeared to bottom out.

- @support_roosevelt
- @support_france
- @stable_currency_out_of_gov
- @salvage
- @no_business


@stable_currency_out_of_gov
title: German representatives call for stable currency, along with Britain and France.
view-if: not spd_in_government
on-arrival: west_relation += 1

As expected, the conservative German bankers support Britain and France's plan to stabilize currency exchange rates. In response to the continued support for fixed currency exchange rates and the impasse on debts, the Americans pull out of the conference, leaving it with no agreements whatsoever.

[? if unemployed >= 15 : Nevertheless, the world economy is slowly improving due in part to the economic stimulus and more liberal trade policies of the Roosevelt administration. ?]
[? if unemployed < 15 : Nevertheless, the world economy is slowly improving due in part to the economic stimulus and more liberal trade policies of the Roosevelt administration as well as our own economic plan. ?]


@support_roosevelt
title: Support Roosevelt's ideas - we must devalue currency.
view-if: spd_in_government


We support Roosevelt's plan for controlled inflation, leaving us at odds with the European powers. In response to the continued support for fixed currency exchange rates by France and Britain and the impasse on debts, the Americans pull out of the conference, leaving it with no agreements whatsoever.

@support_france
title: Support France and Britain - we must have stable currency.
view-if: spd_in_government
on-arrival: west_relation += 1; eu_progress += 1

Our position brings us closer to other European powers. In response to the continued support for fixed currency exchange rates and the impasse on debts, the Americans pull out of the conference, leaving it with no agreements whatsoever.


@salvage
title: Can we try to salvage the negotiations?
view-if: spd_in_government

How should we try to salvage the negotiations?

- @salvage_currency: Try to convince France and Britain that slowly controlled inflation is possible and desirable.
- @salvage_debts: Try to convince the US to reduce war debts for the sake of peace.
- @no_business: Nothing can be done.

@salvage_currency
choose-if: woytinsky_advisor = 1 and wtb_implemented > 1
unavailable-subtitle: Wladimir Woytinsky would be extremely helpful right now.
on-arrival: unemployed -= 2 if unemployed >= 10; unemployed -= 1.5 if unemployed >= 5; inflation += 1.5; workers_spd += 4*(1-dissent); new_middle_spd += 3*(1-dissent); economic_growth += 1.5 if economic_growth < 3

Wladimir Woytinsky, now part of our delegation to the conference, uses our successful implementation of the WTB-plan in order to argue for loosening currency valuation. The argument is at least somewhat convincing, and an agreement is reached, leading to increased inflation and demand stimulus throughout the world.

@salvage_debts
choose-if: reparations <= -2 and pacifism >= 3 and (rearmament_exposed or west_relation >= 3)
unavailable-subtitle: We cannot argue for pacifism in our current state.
on-arrival: budget += 1; unemployed -= 1 if unemployed >= 10; unemployed -= 1 if unemployed >= 5; inflation += 1 if inflation < 0; new_middle_spd += 4*(1-dissent); workers_spd += 3*(1-dissent); economic_growth += 1 if economic_growth < 3

We try to argue that reducing war debts will reduce the risk of a future European war, using our successful disarmament and the reduction in Versailles reparation payments as an argument. The argument is at least somewhat convincing to the Americans, leading to an agreement to reduce debts from the Great War. This indirectly also improves our economy.

@no_business
title: This conference is none of our concern.
view-if: spd_in_government

The conference collapses, as expected. In response to the continued support for fixed currency exchange rates and the impasse on debts, the Americans pull out of the conference, leaving it with no agreements whatsoever.

[? if unemployed >= 15 : Nevertheless, the world economy is slowly improving due in part to the economic stimulus and more liberal trade policies of the Roosevelt administration. ?]
[? if unemployed < 15 : Nevertheless, the world economy is slowly improving due in part to the economic stimulus and more liberal trade policies of the Roosevelt administration as well as our own economic plan. ?]
