title: Reichstag Elections
new-page: true
tags: event
view-if: year >= next_election_year and month >= next_election_month
on-departure: month_actions += 1; in_election = 0; next_election_year = next_election_year + 4; n_elections += 1; next_election_time = time + 48;
set-jump: post_election_1928

= Elections, [+ month : month +] [+ year +]

The Reichstag elections are to be held now.

- @election_algorithm: May we do our best...
- @cancel_elections

@cancel_elections
title: We cannot hold the elections now. Cancel the elections.
subtitle: This is an anti-democratic move that may backfire.
view-if: in_emergency_government == 1 and president == "Braun"

Since we are currently ruling as part of an emergency government, we may use quasi-constitutional trickery to significantly delay these elections, and continue governing via Article 48. This is likely to be extremely unpopular both with our base and with the other parties. However, this may be necessary to prevent the extremists from gaining power.

- @cancel_elections_2: Yes, do it.
- @election_algorithm: No, let's hold the elections.

@cancel_elections_2
on-departure: next_election_year = next_election_year + 1; next_election_time = time + 12; pro_republic -= 6; dvp_relation -= 8; z_relation -= 8; ddp_relation -= 10; kpd_relation -= 8; coup_progress += 2; workers_spd -= 4; new_middle_spd -= 5; old_middle_spd -= 5; rural_spd -= 5; catholics_spd -= 5; unemployed_spd -= 5; coup_progress += 2 if nsdap_normalized > 0.4
achievement: ausnahmezustand

Fine. President [+ president +] has taken the unprecedented step of canceling these elections. New elections are scheduled for [+ next_election_month : month +] [+ next_election_year +].

Let us hope this does not backfire.

@post_election_1928
on-arrival: {!
// set congress proportion to votes
Q.spd_r = Q.spd_votes;
Q.z_r = Q.z_votes;
Q.kpd_r = Q.kpd_votes;
Q.ddp_r = Q.ddp_votes;
Q.dvp_r = Q.dvp_votes;
Q.dnvp_r = Q.dnvp_votes;
Q.nsdap_r = Q.nsdap_votes;
Q.other_r = Q.other_votes;

// The largest party can only be the NSDAP or the SPD.
Q.largest_party = 'SPD';
if (Q.nsdap_r >= Q.spd_r && Q.nsdap_r >= Q.kpd_r) {
    Q.largest_party = 'NSDAP';
}
if (Q.kpd_r >= Q.spd_r && Q.kpd_r >= Q.nsdap_r) {
    Q.largest_party = 'KPD';
}

// coalitions
Q.weimar_coalition = Q.spd_r + Q.ddp_r + Q.z_r - 3;
Q.grand_coalition = Q.spd_r + Q.ddp_r + Q.z_r + Q.dvp_r;
Q.bourgeois_coalition = Q.ddp_r + Q.z_r + Q.dvp_r + Q.other_r;
Q.center_right_coalition = Q.z_r + Q.ddp_r + Q.dvp_r + Q.other_r + Q.dnvp_r;
Q.right_coalition = Q.z_r + Q.dvp_r + Q.other_r + Q.dnvp_r;
Q.far_right_coalition = Q.dnvp_r + Q.nsdap_r;
Q.left_coalition = Q.spd_r + Q.kpd_r;
// this is an absurd concept lol
Q.popular_front_coalition = Q.spd_r + Q.kpd_r + Q.z_r + Q.ddp_r - 3;
Q.anti_democratic_coalition = Q.kpd_r + Q.nsdap_r + Q.dnvp_r;
// aka everyone except nazi, kpd, dnvp
Q.neo_weimar_coalition = Q.spd_r + Q.ddp_r + Q.z_r + Q.dvp_r + Q.other_r;
// Hitler + Right-wing parties (only comes into effect if you've alienated Z too badly)
Q.hitler_right_coalition = Q.z_r + Q.dnvp_r + Q.nsdap_r;
// Progressive coalition isn't going to be implemented as a distinct coalition but it's the coalition that lets you pass improved women's rights/gay rights
Q.progressive_coalition = Q.spd_r + Q.kpd_r + Q.ddp_r;

Q.leverage = Q.spd_r;

Q.grand_coalition_failed = 0;
// Reset government and ministers... Based on Brüning's 1930 govt
Q.coalition_dissent = 0;
Q.kpd_coalition_dissent = 0;
Q.spd_in_government = 0;
Q.in_weimar_coalition = 0;
Q.in_grand_coalition = 0;
Q.in_left_front = 0;
Q.in_popular_front = 0;
Q.in_emergency_government = 0;
Q.spd_toleration = 0;
Q.foreign_minister_party = 'DVP';
Q.interior_minister_party = 'Z';
Q.justice_minister_party = 'I';
Q.labor_minister_party = 'Z';
Q.reichswehr_minister_party = 'I';
Q.economic_minister_party = 'DDP';
Q.finance_minister_party = 'DVP';
Q.agriculture_minister_party = 'Z';

// reset KPD goals
Q.kpd_goals_seen = 0;
Q.kpd_ultimatum_seen = 0;
Q.welfare_goal = 0;
Q.agriculture_goal = 0;
Q.economy_goal = 0;
Q.finance_goal = 0;
Q.labor_goal = 0;
Q.foreign_goal = 0;
Q.interior_goal = 0;
Q.reichswehr_goal = 0;

Q.welfare_goal_completed = 0;
Q.agriculture_goal_completed = 0;
Q.economy_goal_completed = 0;
Q.finance_goal_completed = 0;
Q.labor_goal_completed = 0;
Q.foreign_goal_completed = 0;
Q.interior_goal_completed = 0;
Q.reichswehr_goal_completed = 0;
!}
on-departure: nazi_urgency = 1 if (nazi_urgency == 0 and nsdap_r >= 15)
go-to: coalition_menu

**Election results:**

SPD: [+ spd_r +]%

Z + BVP: [+ z_r +]% (BVP: 3%)

KPD: [+ kpd_r +]%

DDP: [+ ddp_r +]%

DVP: [+ dvp_r +]%

DNVP: [+ dnvp_r +]%

NSDAP: [+ nsdap_r +]%

Other (mostly conservatives): [+ other_r +] %

**Potential coalitions:**

Weimar Coalition (SPD + Z + DDP): [+ weimar_coalition +]%

Grand Coalition (SPD + Z + BVP + DDP + DVP): [+ grand_coalition +]%

Bourgeois Coalition (Z + BVP + DDP + DVP + Others): [+ bourgeois_coalition +]%

Right-wing Coalition (Z + BVP + DVP + Others + DNVP): [+ right_coalition +]%

[? if nazi_urgency <= 1 and nsdap_r >= 15 : The rapid rise in NSDAP votes is extremely worrying. Many had believed that the party was moribound; now, some of us consider it to be the greatest threat to democracy in Germany. ?]

[? if anti_democratic_coalition >= 50 : Together, the anti-democratic KPD, DNVP, and NSDAP have a majority in the Reichstag. A stable governing majority will be almost impossible. ?]

There are some potential coalition arrangements.

@coalition_menu

- @spd_majority
- @weimar_coalition
- @grand_coalition
- @right_coalition
- @far_right_coalition
- @no_majority
- @brauns_dilemma
- @left_coalition
- @popular_front_coalition

# TODO: black-brown coalition


@popular_front_coalition
title: A new "Popular Front" coalition?
subtitle: SPD + KPD + Z + DDP ([+ popular_front_coalition +]%)
unavailable-subtitle: SPD + KPD + Z + DDP ([+ popular_front_coalition +]%) - [? if kpd_relation < 50: We do not have good enough relations with the KPD. ?][? if z_relation < 50: We do not have good enough relations with the Center Party. ?][? if communist_coalition < 3 : The Communists are unwilling to compromise at all. ?]
view-if: popular_front_coalition >= 50
choose-if: kpd_relation >= 50 and z_relation >= 50 and ddp_relation >= 50 and communist_coalition >= 3

Could we really form a coalition that included both the Center and the Communists? It seems like an absurd concept, but given our good relationships with both sides, it is not an impossibility.

The far-right might attempt a coup, if such a coalition were formed.

- @popular_front_2: Form the Popular Front!

@popular_front_2

Somehow, both the Communists and Center are at the very least willing to entertain negotiations. Of course, KPD radicals and Center conservatives derail the negotiations almost immediately. The KPD demands leadership of the coalition for themselves and their chairman Ernst Thälmann, [? if spd_r > kpd_r : despite our higher vote share, ?]while the Center is offended by the prospect of helping Bolsheviks enter government. *Something* must be done to restore balance.

- @popular_front_resources
- @popular_front_democracy
- @popular_front_braun
- @coalition_menu: This is impossible. Try another governing arrangement.

@popular_front_resources
title: Use our resources to smooth things over.
unavailable-subtitle: Required: 4 resources
subtitle: -4 resources
choose-if: resources >= 4
on-arrival: resources -= 4
go-to: popular_front_success

@popular_front_democracy
title: Convince the KPD to accept democracy.
unavailable-subtitle: Our relationship with the KPD is not high enough.
choose-if: kpd_relation >= 60 and communist_coalition >= 3
go-to: popular_front_success

@popular_front_braun
title: Use pressure from President Braun to force the parties to cooperate.
view-if: president == "Braun"
go-to: popular_front_success

@popular_front_success
on-arrival: spd_in_government = 1; in_popular_front = 1; chancellor = "Breitscheid"; chancellor_party = "SPD"; kpd_relation += 10; coup_progress += 2; in_left_front = 0; in_weimar_coalition = 0; in_grand_coalition = 0; coalition_dissent = 0;
go-to: popular_front_civil_war if coup_progress >= 10; popular_front_no_civil_war if coup_progress < 10
achievement: volksfront

Somehow, surprisingly, the coalition is successfully formed! We still need a chancellor; the only acceptable choice among the parties was Rudolf Breitscheid of the SPD, acceptable to the Center [? if president == "Hindenburg" : as well as President Hindenburg, albeit extremely reluctantly ?].

@popular_front_no_civil_war
on-arrival: kpd_goals_seen = 0; finance_minister_party = "Z"; interior_minister_party = "Z"; economy_minister_party = "Z"; labor_minister_party = "Z"; foreign_minister_party = "Z"; agriculture_minister_party = "Z"; reichswehr_minister = "I"; leverage += kpd_r

# TODO: just increase coup progress, don't start a coup right away.

In response to the KPD's entrance into the government, the right-wing takes a "wait-and-see" approach for now. They expect that the Popular Front government will be unstable and incompetent, and will collapse of its own accord, discrediting the left and center and eventually enabling the destruction of the Weimar Republic. Our task is to prove them wrong.

The KPD has taken an abstentionist position with regards to ministries. They claim to be participating as the "loyal opposition" within the Popular Front government, supporting us from the outside while also running their own propaganda campaigns to turn workers to their side. They seem to expect that the Popular Front would inevitably fail, and that our failure would benefit them.

- @ministries: First, we must select our ministries.


@popular_front_civil_war
on-arrival: kpd_relation = 80; reichswehr_loyalty += 0.25;

In response to the KPD's entrance into the government, the right-wing immediately launches a coup attempt. Troops of the Stahlhelm and Nazi SA enter Berlin and immediately begin clashing with our supporters! Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop us from taking power.

The Center manages to diffuse some of the hostility from the Reichswehr and the Stahlhelm, but fail to prevent the outbreak of fighting. We are forced to fight on the same side as the KPD, whether we want to or not.

- @civil_war: A civil war, then.



@far_right_coalition
title: Far-right coalition
subtitle: NSDAP + DNVP ([+ far_right_coalition +]%)
view-if: far_right_coalition >= 50
go-to: far_right_win if year >= 1932 or nsdap_r > 44; far_right_fail if year < 1932

There are enough votes for the Nazis and the DNVP to form a coalition. Hitler has made his participation in government contingent on being appointed chancellor, while Hugenberg of the DNVP does not support his and would rather he be made chancellor. It is up to President [+ president +] to decide.

@far_right_win
on-arrival: chancellor = "Hitler"; chancellor_party = "NSDAP"

[? if president == "Hindenburg" : President Hindenburg's camarilla ?][? if president == "Braun" : President Braun somehow ?] has decided that Hitler must be chancellor[? if president == "Hindenburg": , and the aging president accepts their recommendations ?][? if president == "Braun" :  because it is what the constitution would stipulate ?].

- @hitler_takes_power: What do we do now?


@far_right_fail
on-arrival: chancellor = "Schleicher"; chancellor_party = "I"; pro_republic -= 10; next_election_time = time+5; next_election_month = month + 5; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12;

The reactionary establishment is still very skeptical of Hitler, so instead, [+ president +] appoints Kurt von Schleicher, a former general, as chancellor.

Schleicher's government is deeply unpopular among all parties of the Reichstag. Despite his support for a coalition with the NSDAP, the Nazis want power all to themselves, without Schleicher.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].



@left_coalition
title: Is a United Left coalition possible?
subtitle: SPD + KPD ([+ left_coalition +]%)
unavailable-subtitle: SPD + KPD ([+ left_coalition +]%). [? if kpd_relation < 50 : We do not have adequate relations with the KPD. ?][? if communist_coalition < 3 : The Communists are too committed to their radical ways to partake in government. ?]
view-if: left_coalition >= 50
choose-if: kpd_relation >= 50 and communist_coalition >= 3

There are enough votes for a SPD + KPD coalition government, if such a thing were actually possible.

The far-right might attempt a coup, if such a coalition were formed.

- @left_coalition_impossible: This is impossible.
- @left_coalition_possible: This could be possible.

@left_coalition_impossible
go-to: coalition_menu

A far left coalition is impossible.


@left_coalition_possible

The KPD is still very reluctant to join a coalition, despite grassroots friendliness between our memberships. The Stalinist leadership is vehemently against such a thing. They demand the appointment of Ernst Thälmann as chancellor in exchange for a coalition. [? if president == "Hindenburg" : There is no chance whatsoever that President Hindenburg would approve of the appointment of a die-hard communist as chancellor. ?] Alternatively, if our relations with them are good enough, we could convince them to support the appointment of one of our centrist or left-leaning members as chancellor.

- @thalmann_chancellor: Appoint Thälmann as chancellor.
- @left_coalition_resources
- @left_coalition_relations
- @coalition_menu: Try a different governing arrangement.

@thalmann_chancellor
choose-if: president == "Braun" and reformist_strength + neorevisionist_strength < left_strength
unavailable-subtitle: [? if president != "Braun": President [+ president +] would never allow such a thing. ?] [? if reformist_strength >= left_strength : Most in the parliamentary party would not support Thälmann as chancellor. ?]
on-arrival: chancellor = "Thälmann"; chancellor_party = "KPD"; kpd_relation = 80; in_left_front = 1; spd_in_government = 1 

With great reluctance, President [+ president +] appoints [+ chancellor +] as chancellor.

Immediately, [+ chancellor +] goes to work building a "Soviet Germany": consolidating as much power as possible in the KPD, purging state institutions of anti-communists, and sending street fighters against the "enemies of the people".

In response to [+ chancellor +] being appointed Chancellor, the right-wing forces launches almost immediately launch an attempted coup. Troops of the Stahlhelm and Nazi SA enter Berlin and immediately begin clashing with our supporters! Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop the KPD from taking power. We are forced to be on the same side as the KPD, whether we want to or not.

- @civil_war


@left_coalition_resources
title: Use our resources to smooth things over.
unavailable-subtitle: Required: 3 resources
subtitle: -3 resources
choose-if: resources >= 3
on-arrival: resources -= 3
go-to: left_coalition_success

@left_coalition_relations
title: Convince the KPD to accept democracy.
unavailable-subtitle: Our relationships with the KPD are not good enough.
choose-if: kpd_relation >= 60
go-to: left_coalition_success


@left_coalition_success
on-arrival: spd_in_government = 1; in_left_front = 1; chancellor = "Breitscheid"; chancellor_party = "SPD"; coup_progress += 3; capital_strike_progress += 2; in_popular_front = 0; in_grand_coalition = 0; in_weimar_coalition = 0; kpd_relation += 10
go-to: left_coalition_civil_war if coup_progress >= 10; left_coalition_no_civil_war if coup_progress < 10
achievement: einheitsfront

Somehow, surprisingly, the left coalition is successfully formed, without appointing Thälmann as chancellor. The consensus choice for chancellor, accepted by the SPD, KPD, and President [+ president +], is Rudolf Breitscheid, who belongs to the centrist faction of the SPD, but was acceptable to the left due to his previous membership in the left-wing anti-war USPD.

# just raise the coup limit, don't immediately go to civil war

@left_coalition_no_civil_war
on-arrival: kpd_goals_seen = 0; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_loyalty -= 0.05

In response to the KPD's entrance into the government, the right-wing takes a "wait-and-see" approach for now. They expect that our government will be unstable and incompetent, and will collapse of its own accord, discrediting the left and eventually enabling the destruction of the Weimar Republic. Our task is to prove them wrong.

The KPD has taken an abstentionist position with regards to ministries. They claim to be participating as the "loyal opposition" in the worker's government, supporting us from the outside while also running their own propaganda campaigns to turn workers to their side. They seem to expect that we would inevitably fail, and that our failure would benefit them. 

Whatever their reasoning is, the SPD has control over the ministries of government.

@left_coalition_civil_war
on-arrival: reichswehr_loyalty += 0.1

In response to the KPD's entrance into the government, the right-wing immediately launches a coup attempt. Troops of the Stahlhelm and Nazi SA enter Berlin and begin clashing with our supporters. Reactionary officers within the Reichswehr have taken their troops out of the barracks and joined them with the Stahlhelm to stop us from taking power.

- @civil_war: A civil war, then.




@right_coalition
title: A right-wing coalition may be formed.
subtitle: Z + BVP + DVP + DNVP + Others ([+ right_coalition +]%)
unavailable-subtitle: The DNVP is not interested in a coalition with the Center.
view-if: right_coalition >= 50
choose-if: year < 1928
go-to: no_right_coalition if year >= 1930; right_coalition_2 if year <= 1929

There are enough votes for a right-wing coalition of the Center (+ BVP), DVP, DNVP, and minor conservative parties.

@no_right_coalition
go-to: no_majority

However, the DNVP and its leader Hugenberg is refusing to partake in a coalition with the "November criminals" in the Center Party. Thus, there is no clear path towards a Reichstag majority.



@right_coalition_2
on-arrival: spd_in_government = 0; spd_toleration = 0; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"

Even though the DNVP is very reluctant to participate in such a coalition, given Hugenberg's anti-constitutional views, they still accede to President Hindenburg's wishes. Thus, a majority center-right coalition is formed, with Brüning as chancellor.



@no_majority
title: There is no clear governing majority.
view-if: weimar_coalition < 50 and (grand_coalition < 50 or grand_coalition_failed == 1) and president = "Hindenburg" and far_right_coalition < 50
on-arrival: no_majority_elections += 1
go-to: no_majority_bruning if chancellor = "Brüning"; no_majority_papen if chancellor = "Papen" and no_majority_papen_elections == 0; no_majority_papen_2 if chancellor == "Papen" and no_majority_papen_elections > 0; no_majority_schleicher if chancellor = "Schleicher"; no_majority_spd if chancellor_party = "SPD" or chancellor == "Marx"

# TODO: if there is an anti-democratic coalition majority, you should appoint Papen.

# either new elections, or Hindenburg appoints either Brüning, Papen, Schleicher, or Hitler.

@no_majority_spd
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"

There is no path to an obviously workable majority. Since President Hindenburg is unwilling to work with a minority or caretaker SPD government, his preferred chancellor is Heinrich Brüning of the Center Party, who could lead a "presidential" government.

- @toleration_bruning: We will tolerate Brüning.
- @new_grand_coalition: We will try to form a new grand coalition.
- @new_elections: We will have new elections.

# - if NSDAP is the largest party and the 

@no_majority_bruning
go-to: no_majority_bruning_toleration if (no_majority_elections <= 3 and anti_democratic_coalition <= 50); hindenburg_appoints_papen if no_majority_elections > 3 or anti_democratic_coalition > 50


@hindenburg_appoints_papen
on-arrival: chancellor = "Papen"; chancellor_party = "I"; next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; sa_banned = 0; pro_republic -= 8

There is no path to a majority government. President Hindenburg is tired of the chaos, and appoints Franz von Papen as chancellor instead of Brüning. Papen is a indenpendent formerly from the Center Party, and is significantly to the right of the majority of the Center Party.

Papen is immediately faced with a vote of no confidence, given his unpopularity. However the SPD votes, he is guaranteed to fall, due to the unanimous hatred against him from all other parties.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@no_majority_bruning_toleration
on-arrival: no_majority_bruning_elections += 1

There is no path to a majority government. One option would be to have a continuation of the current Brüning government; if we tolerate it, then it would retain a majority vote against no-confidence.

Alternatively, we could try to form a "new grand coalition" of all constitutional parties except the NSDAP, KPD, and DNVP. The stumbling block are the minor conservative parties such as the "Economic Party of the Middle Class", who detest any hint of socialism and would ruin the country before working with Marxists.

The third option is to have new elections.

- @toleration_bruning: We will tolerate Brüning.
- @new_grand_coalition: We will try to form a new grand coalition.
- @new_elections: We will have new elections.


@new_grand_coalition
title: A "New Grand Coalition" of all the constitutional parties?
subtitle: SPD + Z + BVP + DDP + DVP + Others ([+ neo_weimar_coalition +]%)
view-if: neo_weimar_coalition >= 50 and grand_coalition < 50
choose-if: z_relation >= 50 and dvp_relation >= 30
unavailable-subtitle: SPD + Z + BVP + DDP + DVP + Others ([+ neo_weimar_coalition +]%) - [? if z_relation < 50 : We do not have adequate relations with the Center Party. ?][? if dvp_relation < 30 : We do not have adequate relations with the DVP. ?]

How do we convince vehemently anti-socialist parties to allow us to form a majority coalition? Our largely benevolent relations with the Center and DVP may allow us some leverage.

- @use_resources
- @toleration_bruning: No. We will tolerate a minority government.
- @new_elections: No. We will have new elections.

@use_resources
title: Use our resources to smooth over the creation of a coalition.
subtitle: -2 resources
unavailable-subtitle: Not enough resources (need 2).
choose-if: resources >= 2
on-arrival: resources -= 2; spd_in_government = 1; in_grand_coalition = 1
achievement: constitutional_coalition

We transfer some of our campaign funds to our putative coalition partners. Some would call this corruption, but perhaps it is necessary to save our democracy.

Now, we have a new grand coalition, a "Constitutional Coalition" of all the non-fascist and non-communist parties.

Who should be chancellor?

- @join_bruning: For better or for worse, it must be Heinrich Brüning of the Center.
- @braun_chancellor_new: Otto Braun for Chancellor?

@braun_chancellor_new
subtitle: This will reduce our leverage in the ministries.
on-arrival: leverage -= 10; chancellor = "Braun"; chancellor_party = "SPD"
go-to: ministries

We have assembled a new coalition under Otto Braun.


@no_majority_papen
on-arrival: pro_republic -= 10; next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; workers_nsdap += 6; unemployed_nsdap += 6; old_middle_nsdap += 5; new_middle_nsdap += 5; rural_nsdap += 5; no_majority_papen_elections += 1; workers_spd -= 8 if pro_republic < 45; workers_spd *= 0.85 if pro_republic < 40; workers_nsdap += 5 if pro_republic < 40;

After the elections, Papen is still deeply unpopular, without any hint of coalition support, and there is a vote of no confidence called against him almost immediately. No matter how we vote, the measure would pass. The Reichstag is dissolved once more.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].


@no_majority_papen_2
on-arrival: chancellor = "Schleicher"; pro_republic -= 10; next_election_time = time+5; next_election_month = month + 5; next_election_year = year; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; workers_nsdap += 5; unemployed_nsdap += 10; old_middle_nsdap += 5; new_middle_nsdap += 5; rural_nsdap += 10; workers_spd -= 8 if pro_republic < 45; workers_spd *= 0.85 if pro_republic < 40; workers_nsdap += 5 if pro_republic < 40;

There is no path to a majority government. Hindenburg is displeased with Papen, and calls on him to resign. Now, former army general Kurt von Schleicher has been appointed chancellor, again as part of a presidential government.

Schleicher's government is deeply unpopular among all parties of the Reichstag. Despite his support for a coalition with the NSDAP, the Nazis want power all to themselves, without Schleicher.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].


@no_majority_schleicher
go-to: hitler_chancellor


@hitler_chancellor
on-arrival: chancellor = "Hitler"; chancellor_party = "NSDAP"

There is no path to a majority government for now. The scheming around President Hindenburg is leading to only one path - that of Chancellor Adolf Hitler. [? if largest_party == "NSDAP" : Since they are the largest party, it is not exactly contrary to parliamentary procedure, so it is difficult to raise objections solely on procedural grounds. ?]

- @hitler_takes_power: What now?

# if NSDAP is largest party and Hindenburg is president, then Hindenburg appoints Hitler chancellor.

@spd_majority
title: SPD majority alone
subtitle: SPD: [+ spd_r +]%
view-if: spd_r >= 50
on-arrival: chancellor = "Empty"; chancellor_party = "SPD"; spd_in_government = 1; in_spd_majority = 1; pro_republic += 5; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_loyalty -= 0.05; 
achievement: majority_party

Finally, the SPD can govern alone! Our long campaign of electioneering has paid off. We do not need to worry about coalition government; anything is possible. 

Who should be chancellor?

- @braun_majority_chancellor: Otto Braun for Chancellor.
- @breitscheid_majority_chancellor: Rudolf Breitscheid for Chancellor.
- @muller_majority_chancellor: Hermann Müller for Chancellor.

@braun_majority_chancellor
on-arrival: chancellor = "Braun"

[? if president == "Braun": Our experts have checked the constitution, and have discovered that there is no specific prohibition against the Chancellor also being the President (the President may not be a member of the Reichstag; there is no law saying that the Chancellor must be a member of the Reichstag). Thus, we have Chancellor-President Otto Braun. ?]
[? if president != "Braun": Otto Braun has become chancellor of the SPD majority. ?]

@breitscheid_majority_chancellor
on-arrival: chancellor = "Breitscheid"

Rudolf Breitscheid has become chancellor of the SPD majority.

@muller_majority_chancellor
view-if: muller_dead = 0
on-arrival: chancellor = "Müller"

Hermann Müller has become chancellor of the SPD majority.



@brauns_dilemma
title: There is no clear governing majority.
subtitle: President Braun faces a dilemma.
view-if: president == "Braun" and (grand_coalition < 50 or grand_coalition_failed == 1)

# if Otto Braun is president and there is no workable majority... then you're fucked.

President Braun faces a dilemma. He desperately desires a socialist or at least democratic government, but the electoral results are not there for such. Article 48 has been used by Hindenburg to promulgate anti-democratic and anti-worker policies; can Article 48 be used for good as well as evil?

- @emergency_braun: We will form an emergency government with Braun's support.
- @new_elections: We will have new elections.
- @new_grand_coalition
- @left_coalition
- @popular_front_coalition
- @far_right_coalition



@emergency_braun
title: We will form an emergency government with Braun's support.
subtitle: This may have the effect of inflamming public sentiment against us, and the reactionaries will be against us each step of the way.
on-arrival: sa_strength += 100; sh_strength += 100; in_emergency_government = 1;

Yes, an emergency minority government, with the support of the SPD. Who should lead this emergency minority government?

- @braun_emergency_chancellor: Otto Braun for Chancellor-President!
- @breitscheid_emergency_chancellor: Rudolf Breitscheid for emergency Chancellor.
- @bruning_emergency_chancellor: Brüning of the Center Party, as a goodwill gesture toward the bourgeoisie.

@braun_emergency_chancellor
subtitle: This is unprecedented.
on-arrival: chancellor = "Braun"; chancellor_party = "SPD"; spd_in_government = 1; pro_republic -= 5; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_loyalty -= 0.1; 

Our experts have checked the constitution, and have discovered that there is no specific prohibition against the Chancellor also being the President (the President may not be a member of the Reichstag; there is no law saying that the Chancellor *must* be a member of the Reichstag).

Thus, we have Chancellor-President Otto Braun. He claims that he will resign as soon as the economic-political crisis is over. The right-wing parties, who were once clamoring to replace parliamentary rule with dictatorship or monarchy, are now clamoring for the restoration of democracy.

- @far_right_coup
- @emergency_no_confidence: The Nazis immediately call a no-confidence vote against us...

@breitscheid_emergency_chancellor
subtitle: The Communists are warming to him...
on-arrival: chancellor = "Breitscheid"; chancellor_party = "SPD"; spd_in_government = 1; kpd_relation += 5; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_loyalty -= 0.1;

We have appointed Rudolf Breitscheid as emergency chancellor. The Communists consider him less objectionable than the alternatives, although it is unclear whether they will tolerate this government.

- @far_right_coup
- @emergency_no_confidence


@bruning_emergency_chancellor
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"; spd_in_government = 1; z_relation += 10; foreign_minister_party = "SPD"; interior_minister_party = "SPD"; justice_minister_party = "SPD"; labor_minister_party = "SPD"; reichswehr_minister_party = "SPD"; economic_minister_party = "SPD"; finance_minister_party = "SPD"; agriculture_minister_party = "SPD" 

The Center Party is pleased, and somewhat surprised, that we have appointed [+ chancellor +] as chancellor.

- @far_right_coup
- @emergency_no_confidence

@far_right_coup
title: The far-right parties immediately launch a military coup against us...
view-if: far_right_coalition >= 45
on-arrival: sh_strength += 150; sa_strength += 150; sh_militancy += 0.05; sa_militancy += 0.05

The far-right parties, seeing that we are willing to deny them government by questionably democratic means, have decided that they can only take power by undemocratic means, and have risen up in force against the sitting government.

Their numbers are bolstered by the many government workers who are against the prospect of a unified Socialist government. Police and soldiers defect to the SA and Stahlhelm.

- @civil_war: It is war.


@emergency_no_confidence
title: The Nazis immediately call a no-confidence vote against us...
view-if: far_right_coalition < 45
on-arrival: z_no_confidence = 1 if z_relation < 45; kpd_no_confidence = 1 if kpd_relation < 45; others_no_confidence = 1 if (z_relation + dvp_relation)/2 < 45; sa_strength += 200; sh_strength += 200; no_confidence_votes = nsdap_r + dnvp_r; no_confidence_votes += z_r if z_no_confidence; no_confidence_votes += kpd_r if kpd_no_confidence; no_confidence_votes += other_r if other_no_confidence; no_confidence_succeeds = 1 if no_confidence_votes > 50

The NSDAP and DNVP will vote against us, of course. The other parties vote their conscience.

In total, [+ no_confidence_votes +]% of the Reichstag is against us. [? if no_confidence_succeeds : We have lost the vote of no confidence. ?]

- @no_confidence_elections_new: There will be new elections, which we will try to delay.
- @no_confidence_win: We have won, for now.


@no_confidence_elections_new
on-arrival: {!
Q.no_majority_elections += 1;
Q.next_election_month = Q.month + 6;
Q.next_election_time = Q.time + 6;
if (Q.next_election_month > 12) {
    Q.next_election_month -= 12;
    Q.next_election_year = Q.year + 1;
} else {
    Q.next_election_year = Q.year;
}
Q.pro_republic -= 5;
Q.workers_spd -= 3;
Q.new_middle_spd -= 3;
Q.old_middle_spd -= 3;
Q.rural_spd -= 3;
Q.catholics_spd -= 3;
Q.workers_nsdap += 5;
Q.old_middle_nsdap += 3;
Q.new_middle_nsdap += 3;
Q.rural_nsdap += 3;
Q.unemployed_nsdap += 5;
if (pro_republic < 45 && pro_republic >= 40) {
    Q.workers_spd -= 6;
}
if (Q.pro_republic < 40) {
    Q.workers_spd *= 0.85;
    Q.new_middle_spd -= 5;
    Q.old_middle_spd -= 5;
    Q.rural_spd -= 5;
    Q.catholics_spd -= 3;
    Q.workers_nsdap += 5;
    Q.old_middle_nsdap += 5;
    Q.new_middle_nsdap += 5;
    Q.rural_nsdap += 5;
    Q.unemployed_nsdap += 5;   
}
!}

The next elections are set for [+ next_election_month : month +] [+ next_election_year +].


@no_confidence_win

We have won the vote. Now our task is to fix the country.


@weimar_coalition
title: We can form a Weimar Coalition.
subtitle: SPD + Z + DDP ([+ weimar_coalition +]%)
view-if: weimar_coalition >= 50
on-arrival: leverage += 15; in_weimar_coalition = 1; spd_in_government = 1; in_popular_front = 0; in_left_front = 0
achievement: weimar_coalition

We have enough votes to form a "Weimar Coalition" government consisting of the SPD, Center (Z) and Democratic (DDP) parties! This has been our preferred governing coalition.

Who should be the chancellor?

- @muller
- @braun
- @breitscheid_coalition_chancellor
- @z_chancellor

@breitscheid_coalition_chancellor
title: Rudolf Breitscheid for Chancellor
subtitle: Breitscheid is viewed as friendlier towards the left of the SPD.
view-if: year >= 1930
on-arrival: chancellor = "Breitscheid"; chancellor_party = "SPD";
go-to: ministries

Rudolf Breitscheid has been appointed chancellor.


@grand_coalition
title: We can form a Grand Coalition.
view-if: grand_coalition >= 50 and grand_coalition_failed == 0
subtitle: SPD + Z + BVP + DDP + DVP
on-arrival: in_grand_coalition = 1; in_popular_front = 0; in_left_front = 0
go-to: grand_coalition_spd_lead if (n_elections <= 1 and largest_party == "SPD") or (largest_party == "SPD" and spd_r >= 30); grand_coalition_z_lead if (n_elections > 1 and spd_r < 30) or (n_elections <= 1 and largest_party != "SPD")

# TODO: if there are enough votes to form a grand coalition, who leads? Does the SPD lead or does Brüning (Z) lead? In reality this depended on Hindenburg's will, basically, which in turn depended on what his camarilla supported.
# In the first election, SPD always leads. In subsequent elections, if spd_r >= 29 then SPD leads, otherwise Z leads?

# TODO:

@grand_coalition_z_lead
go-to: grand_coalition_z_lead_2 if dvp_relation >= 30; grand_coalition_fail if dvp_relation < 30

There are enough votes to form a majority coalition consisting of the SPD, Center (Z), Bavarian People's (BVP), Democratic (DDP), and People's (DVP) parties. Given our rather weak electoral performance, [? if spd_no_confidence = 1 : and our previous vote of no confidence against the government, ?][? if no_confidence_against_spd = 1 : and the vote of no confidence against us, ?] President Hindenburg has asked the Center Party to take the lead role in forming a government.

# TODO: if the SPD participated in bringing down the previous government via a vote-of-no-confidence, or if we were essentially removed from government by our coalition partners, we should be more reluctant to participate in a new government.

@grand_coalition_z_lead_2

The Center chancellor is likely to be Heinrich Brüning of the Center Party[? if chancellor = "Brüning" : again ?]. He is known for his conservatism and sympathy for monarchism, being on the right wing of the Center. [? if austerity >= 2 : Due to the government's austerity policies, many of our members are reluctant to participate in this coalition again. Some support a "toleration" arrangement in which we will provide outside support to prevent the far-right from either entering government or calling a successful no-confidence vote. Others support entering into pure opposition, and letting the right do what it wishes. ?]

Brüning is not opposed to a coalition with us. Alternatively, we could enter a "toleration" agreement.

- @join_bruning: Join the Brüning coalition.
- @bruning_oppo: Join the opposition.
- @toleration_bruning: We will "tolerate" a minority Brüning government.

@join_bruning
view-if: grand_coalition >= 50 or neo_weimar_coalition >= 50
on-arrival: spd_in_government = 1; spd_toleration = 0; in_grand_coalition = 1; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"; leverage = 25 if leverage >= 25;
go-to: ministries

Heinrich Brüning welcomes us into his new cabinet. Now, we have our pick of ministries.

@bruning_oppo
on-arrival: spd_in_government = 0; spd_toleration = 0; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"

By refusing to join the grand coalition or tolerating it, we essentially throw the balance of government to the right wing. [? if right_coalition >= 50 : There are enough votes to form a right-wing coalition that includes the DNVP. However, the DNVP leader, Hugenberg, is opposed to a coalition with the "November criminals" of the Center Party. And of course, the NSDAP is not interested in coalition government. This means that there will not be enough votes to form a majority coalition. ?][? if right_coalition < 50 : There are not enough votes to form a right-wing coalition that includes the DNVP, and they are not interested in joining in a coalition with the "November criminals" of the Center Party, anyhow. And of course, the NSDAP is not interested in coalition government. This means that there is no route to a majority coalition. ?]

If we do not at least "tolerate" the government, there will be new elections in a few months. In the meantime, the President and Chancellor may use the emergency powers granted by Article 48 of the constitution to essentially govern by decree. And it is not at all clear that we will do any better in the next election.

- @join_bruning: Join the Brüning coalition.
- @toleration_bruning: We will tolerate Brüning after all.
- @new_elections: New elections, it is!


@new_elections
on-arrival: {!
Q.next_election_month = Q.month + 3;
Q.next_election_time = Q.time + 3;
if (Q.next_election_month > 12) {
    Q.next_election_month -= 12;
    Q.next_election_year = Q.year + 1;
} else {
    Q.next_election_year = Q.year;
}
if (Q.chancellor == "Brüning") {
    Q.z_relation -= 5;
    Q.dvp_relation -= 5;
}
Q.pro_republic -= 8;
Q.workers_spd -= 5;
Q.new_middle_spd -= 5;
Q.old_middle_spd -= 5;
Q.rural_spd -= 5;
Q.catholics_spd -= 3;
Q.new_middle_dvp -= 8;
Q.old_middle_dvp -= 8;
Q.new_middle_ddp -= 5;
Q.old_middle_ddp -= 10;
Q.rural_dnvp -= 5;
Q.workers_nsdap += 5;
Q.old_middle_nsdap += 6;
Q.new_middle_nsdap += 6;
Q.rural_nsdap += 6;
Q.unemployed_nsdap += 5;
if (Q.pro_republic < 45) {
    // TODO: if you pursue a consistently revolutionary course, you might avoid this? But you might also cause a coalition between Hitler and the Center.
    Q.z_relation -= 5;
    Q.dvp_relation -= 5;
    Q.workers_spd *= 0.85;
    Q.unemployed_spd -= 2;
    Q.new_middle_spd -= 5;
    Q.old_middle_spd -= 5;
    Q.rural_spd -= 5;
    Q.catholics_spd -= 3;
    Q.workers_nsdap += 10;
    Q.old_middle_nsdap += 10;
    Q.new_middle_nsdap += 10;
    Q.rural_nsdap += 10;
    Q.unemployed_nsdap += 10;   
}
!}

There is no path to a workable Reichstag majority or minority government. The next elections are set for [+ next_election_month : month +] [+ next_election_year +].

The people's faith in democracy is shaken by this ordeal. Of course, the Nazis are exploiting the instability to claim that democracy is unworkable, and that Germany needs a strong leader.


@toleration_bruning
on-arrival: spd_in_government = 0; spd_toleration = 1; minority_government = 1; chancellor = "Brüning"; chancellor_party = "Z"; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"; pro_republic -= 4; new_middle_dvp -= 5; new_middle_ddp -= 5; old_middle_dvp -= 5; old_middle_ddp -= 5; old_middle_nsdap += 5; new_middle_nsdap += 5; rural_dnvp -= 5;

We have decided to tolerate Brüning's government. This means that we will vote against no-confidence votes to make sure they fail, but also that we will not be directly participating as ministers in the government. Thus, we can prevent a new election and stop the far right from entering government.

@grand_coalition_fail
on-arrival: grand_coalition_failed = 1

Unfortunately, the DVP and BVP are unwilling to enter into another coalition with the SPD, so the grand coalition is unable to be formed.

- @no_majority: Thus, there is no clear majority.
- @brauns_dilemma
- @right_coalition: Perhaps a right-wing coalition may form.
- @beg_dvp: Perhaps we could ask the DVP to change their minds?
- @coalition_menu: What are the other governing arrangements?


@beg_dvp
view-if: dvp_relation >= 15
go-to: dvp_conservative if stresemann_dead == 1; dvp_majority if stresemann_dead == 0

@dvp_majority
go-to: join_bruning

Under the influence of Gustav Stresemann, the DVP agrees to allow us into the coalition, and so does the BVP.


@dvp_conservative

The DVP asks us to adhere to their conservative economic policies (that is, extreme austerity and labor discipline) in exchange for us joining the coalition.

- @beg_dvp_2: But they would not have a majority without the SPD! The DNVP and NSDAP have no interest in participating in government.
- @no_majority: There is no chance that we would agree.
- @brauns_dilemma
- @coalition_menu: What are the other governing arrangements?


@beg_dvp_2
go-to: beg_dvp_succeed; beg_dvp_fail

We make the case that without the SPD, there is no parliamentary majority, given the DNVP and NSDAP's hostility to constitutional government.

@beg_dvp_succeed
go-to: join_bruning

Somehow, we succeed in convincing them! We are now in a coalition.


@beg_dvp_fail
go-to: no_majority

As expected, the DVP is too set in their anti-democratic ways.



@grand_coalition_spd_lead
on-arrival: leverage -= 15 if dvp_relation < 20

As expected, the SPD is the largest party, and this time, we are expected to take the lead role in the formation of the next government, including selecting the chancellor. President Hindenburg has asked us to form a majority coalition with the Center (Z), Bavarian People's (BVP), Democratic (DDP), and People's (DVP) parties.

[? if dvp_relation < 20 : As our relation with the DVP is poor, they are demanding more minister posts in order to remain part of the coalition. ?]

Who should be the chancellor?

- @muller
- @braun
- @z_chancellor
- @no_govt


@muller
title: Hermann Müller for Chancellor
view-if: muller_dead = 0
subtitle: Muller is popular within the party, and is the preferred candidate of the Center and other parties.
on-arrival: spd_in_government = 1; chancellor = "Müller"; chancellor_party = "SPD"; in_grand_coalition = 1 if in_weimar_coalition == 0
go-to: ministries

Müller's appointment as chancellor is approved by all our potential allies.

@braun
title: Otto Braun for Chancellor
subtitle: Braun is currently the Minister-President of Prussia, and is one of our most popular leaders. However, other parties may have qualms about concentrating too much power in one man.

The Center party has significant qualms about appointing Braun as chancellor, given that he is already Minister-President of Prussia. They ask for either more cabinet positions, or for Braun to resign his state government role. The latter is completely unacceptable.

- @muller
- @braun_2: Let the Center have their cabinet positions.

@braun_2
subtitle: Otto Braun will be Chancellor.
on-arrival: spd_in_government = 1; chancellor = "Braun"; chancellor_party = "SPD"; leverage -= 10; in_grand_coalition = 1 if in_weimar_coalition == 0
go-to: ministries

We have promised to let the Center have more influence in the government in response to the appointment of Braun as chancellor.


@z_chancellor
title: Let the Center party have their chancellor.
subtitle: This is unexpected and will cause internal dissent, but may give us a better position in negotiating for other cabinet positions.
on-arrival: left_dissent += 5; center_dissent += 5; labor_dissent += 5; z_relation += 10; dvp_relation += 5; kpd_relation -= 5; in_grand_coalition = 1 if in_weimar_coalition == 0; chancellor = "Brüning"; chancellor_party = "Z"; spd_in_government = 1; leverage += 5
go-to: ministries

This is highly unexpected, and many in the left and center of our party are criticizing this decision.

Since Wilhelm Marx resigned, Heinrich Brüning has been appointed chancellor.

# TODO

@no_govt
title: We do not form the government.
subtitle: This will cause significant internal dissent, and will lead to increased distrust from the bourgeois parties.
on-arrival: reformist_dissent += 5; center_dissent += 5; labor_dissent += 5; neorevisionist_dissent += 5

Are we sure about not participating in government? This will cause significant internal dissent, and will lead to increased distrust from the bourgeois parties. Even debating this option causes internal backlash. In addition, it is possible that the bourgeois and right-wing parties will also be unable to form a coalition government, leading to a regime appointed by President Hindenburg.

- @muller
- @braun
- @z_chancellor
- @no_govt_goals: Yes, we're sure.

@no_govt_goals

What are our goals here, exactly?

- @majority: We hope to reach a socialist majority in the next election.
- @trap: It is a trap to form a government now. We would be unable to accomplish our goals.
- @socialism: Revolutionary socialism is our goal.

@majority
go-to: no_govt_2

@trap
on-arrival: radicalization += 1
go-to: no_govt_2

@socialism
go-to: no_govt_2


@no_govt_2
on-arrival: reformist_dissent += 25; center_dissent += 10; labor_dissent += 25; neorevisionist_dissent += 25; in_weimar_coalition = 0; in_grand_coalition = 0; chancellor = "Brüning" if chancellor == "Marx"; spd_in_government = 0
go-to: right_coalition_post_spd if right_coalition >= 50; new_elections_spd if right_coalition < 50


@right_coalition_post_spd
on-arrival: chancellor = "Brüning"; new_middle_spd -= 3; old_middle_spd -= 2; workers_spd -= 2; catholics_spd -= 4; next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year = year + 1 if next_election_month > 12; next_election_month = next_election_month - 12 if next_election_month > 12; 

There are enough votes to form a right-wing coalition of the Z, DVP, DNVP, and various minor parties, essentially replicating the previous Marx government. However, the DNVP is very reluctant to join the government[? if year <= 1928 : , as it had just exited from a coalition with the Center. ?][? if year > 1928 : , as its leader, Alfred Hugenberg, is against the very existence of parliamentary democracy, and is unwilling to join such a coalition anymore. ?]

Chancellor Heinrich Brüning of the Center Party currently rules on an emergency basis, as part of a "presidential government". New elections have been called for [+ next_election_month : month +] [+ next_election_year +]


@new_elections_spd
on-arrival: next_election_time = time+3; next_election_month = month + 3; next_election_year = year; next_election_year = year + 1 if next_election_month > 12; next_election_month = next_election_month - 12 if next_election_month > 12; new_middle_spd -= 5; old_middle_spd -= 5; workers_spd -= 3; catholics_spd -= 3; in_election = 1

Since the remaining bourgeois and right-wing parties cannot form a tolerable governing agreement, new elections have been called for [+ next_election_month : month +] [+ next_election_year +]. Wilhelm Marx has resigned, and has been replaced by Heinrich Brüning, also of the Center Party, ruling as part of a presidential cabinet. 

We are seen as responsible for the failure to form a government, and many of our middle-class and even some working-class supporters have deserted the party.


@ministries

We can pick from any number of ministries. Controlling a ministry allows us to directly effect policy in that area. [? if in_left_front : Any ministry that we do not take will be given to the KPD. ?] [? if in_left_front and reichswehr_minister_party == "KPD" : If the KPD controls the Reichswehr Ministry, the army will most likely revolt. ?]

- #mins_1928
- #drop_mins_1928

@labor
title: Labor Ministry
subtitle: Taking the Labor Ministry is expected of us.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 5
view-if: labor_minister_party != "SPD"
on-arrival: leverage -= 5; labor_minister_party = "SPD"; labor_minister = "Wissell"
go-to: ministries

Rudolf Wissell of the SPD has been appointed labor minister.

@interior
title: Interior Ministry
subtitle: Taking the Interior Ministry is expected of us. The Interior Ministry controls domestic security.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 5
view-if: interior_minister_party != "SPD"
on-arrival: leverage -= 5; interior_minister_party = "SPD"; interior_minister = "Severing"
go-to: ministries

Carl Severing of the SPD has been appointed Interior Minister.

@finance
title: Finance Ministry
subtitle: The Finance Ministry controls the government budget and taxes.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 10 
view-if: finance_minister_party != "SPD"
on-arrival: leverage -= 10; finance_minister_party = "SPD"; finance_minister = "Hilferding"
go-to: ministries

Rudolf Hilferding of the SPD has been appointed Finance Minister.

@economic
title: Economic Ministry
subtitle: The Economic Ministry controls broader economic policy.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 10 
view-if: economic_minister_party != "SPD"
on-arrival: leverage -= 10; economic_minister_party = "SPD"; economic_minister = "Schmidt"
go-to: ministries

Robert Schmidt of the SPD has been appointed Economic Minister.

@justice
title: Justice Ministry
subtitle: The Justice Ministry controls the courts.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 10 
view-if: justice_minister_party != "SPD"
on-arrival: leverage -= 10; justice_minister_party = "SPD"; justice_minister = "Radbruch"
go-to: ministries

Gustav Radbruch of the SPD has been appointed Justice Minister.

@foreign
title: Foreign Ministry
subtitle: This is usually the domain of Gustav Stresemann of the DVP.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 10 
view-if: foreign_minister_party != "SPD"
on-arrival: leverage -= 10; foreign_minister_party = "SPD"; foreign_minister = "Breitscheid"
go-to: ministries

Rudolf Breitscheid of the SPD has been appointed Foreign Minister.

# TODO: do we even need the agricultural ministry? Temporarily removing this.

@agriculture
title: Agricultural Ministry
subtitle: This is not our usual domain.
unavailable-subtitle: We do not have enough leverage.
choose-if: leverage >= 10 
view-if: agriculture_minister_party != "SPD"
on-arrival: leverage -= 10; agriculture_minister_party = "SPD"; agriculture_minister = "Baade"
go-to: ministries
tags: mins_1928

Fritz Baade of the SPD has been appointed Agriculture Minister.

@reichswehr
title: Reichswehr Ministry
subtitle: The Ministry responsible for the armed forces is not typically our domain; General Groener and the bourgeois-conservatives will be displeased.
unavailable-subtitle: We do not have enough leverage.
tags: mins_1928
choose-if: leverage >= 15 
view-if: reichswehr_minister_party != "SPD"
on-arrival: leverage -= 15; reichswehr_minister_party = "SPD"; reichswehr_minister = "Leber"
go-to: ministries

Julius Leber of the SPD has been appointed Reichswehr Minister.



@no_more
title: No more ministries.
subtitle: [? if leverage >= 10 : We still have some more leverage to gain ministries. ?][? if leverage < 10 : We do not have much leverage left. ?]
tags: mins_1928

This coalition arrangement is tolerable to all.

@drop_labor
title: Drop the Labor Ministry.
view-if: labor_minister_party = "SPD"
on-arrival: labor_minister_party = "Z"; labor_minister = "Stegerwald"; leverage += 5;
tags: drop_mins_1928
go-to: ministries

@drop_interior
title: Drop the Interior Ministry.
view-if: interior_minister_party = "SPD"
on-arrival: interior_minister_party = "Z"; interior_minister = "Wirth"; leverage += 5;
tags: drop_mins_1928
go-to: ministries

@drop_finance
title: Drop the Finance Ministry.
view-if: finance_minister_party = "SPD"
on-arrival: finance_minister_party = "DVP"; finance_minister = "Moldenhauer"; leverage += 10; finance_minister_party = "DDP" if in_weimar_coalition or in_popular_front;
tags: drop_mins_1928
go-to: ministries

@drop_economic
title: Drop the Economic Ministry.
view-if: economic_minister_party = "SPD"
on-arrival: economic_minister_party = "DVP"; economic_minister = "Curtius"; leverage += 10; economic_minister_party = "Z" if in_weimar_coalition or in_popular_front;
tags: drop_mins_1928
go-to: ministries

@drop_justice
title: Drop the Justice Ministry.
view-if: justice_minister_party = "SPD"
on-arrival: justice_minister_party = "DDP"; justice_minister = "Koch-Weser"; leverage += 10;
tags: drop_mins_1928
go-to: ministries

@drop_foreign
title: Drop the Foreign Ministry.
view-if: foreign_minister_party = "SPD"
on-arrival: foreign_minister_party = "DVP"; foreign_minister = "Stresemann"; leverage += 10; foreign_minister_party = "Z" if in_weimar_coalition or in_popular_front;
tags: drop_mins_1928
go-to: ministries

@drop_agriculture
title: Drop the Agriculture Ministry.
view-if: agriculture_minister_party = "SPD"
on-arrival: agriculture_minister_party = "DDP"; agriculture_minister = "Dietrich"; leverage += 10;
tags: drop_mins_1928
go-to: ministries

@drop_reichswehr
title: Drop the Reichswehr Ministry.
view-if: reichswehr_minister_party = "SPD"
on-arrival: reichswehr_minister_party = "I"; reichswehr_minister = "Groener"; leverage += 15;
tags: drop_mins_1928
go-to: ministries
