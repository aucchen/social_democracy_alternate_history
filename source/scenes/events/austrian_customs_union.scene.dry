title: Austrian customs union
subtitle: The Foreign Ministry seeks closer ties to Austria...
new-page: true
on-arrival: customs_union_seen = 1
tags: event
view-if: year = 1931 and month >= 3 and foreign_minister_party != "SPD" and foreign_minister_party != "KPD" and customs_union == 0 and not eu
max-visits: 1

# TODO: this should happen regardless of the current state of the government...

= Austrian Customs Union

[? if spd_in_government: The [+ foreign_minister_party +] foreign minister has a proposal for a customs union with Austria. We can either accept or reject this agreement. ?]
[? if not spd_in_government: The government of Chancellor [+ chancellor +] has a proposal for a customs union with Austria. Most likely, this will happen whatever we decide. ?]


The nationalist right has long yearned for closer ties with Austria, with the eventual goal of the merger of the two German-speaking countries. A customs union with Austria is the first step towards this goal. Additionally, many believe that a union with Austria would improve our current economic circumstances.

However, the Allies, especially France, are likely to object. This agreement would break the terms of the Versailles Treaty, and could lead to economic sanctions, further hurting our economy.

- @let_it_happen: Let it happen.
- @end_coalition: Stop this arrangement!
- @end_toleration: Attempt to stop this arrangement!

@let_it_happen
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2

The customs union has been formed. Immediately, France and other neighboring countries lodge their protests. The longer-term response is yet to be seen.

@end_coalition
view-if: spd_in_government = 1
on-arrival: coalition_dissent += 1; dvp_relation -= 5; z_relation -= 4; z_relation -= 3 if chancellor_party == "Z"; dvp_relation -= 3 if chancellor_party == "Z"

We attempt to impress upon our coalition partners the gravity of the potential Allied response. The proposal is dead, although the DVP and Center are not pleased.


@end_toleration
view-if: spd_toleration = 1
subtitle: This is difficult; all of the right-of-center parties support this policy.
go-to: end_toleration_success if spd_r + kpd_r + ddp_r >= 50; end_toleration_fail if spd_r + kpd_r + ddp_r < 50

@end_toleration_success
on-arrival: next_election_time = time+3 if next_election_time >= time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0;

With the support of the KPD and DDP/DStP, we have successfully stopped the formation of the customs union. This results in the fall of the [+ chancellor +] government.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@end_toleration_fail
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2

Unfortunately, the far-right parties united with the government to outvote us. The customs union will be formed.
