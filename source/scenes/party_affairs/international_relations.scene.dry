title: International Relations
new-page: true
is-card: true
tags: party_affairs
view-if: international_relations_timer <= 0
on-arrival: month_actions += 1; international_relations_timer = 12;
card-image: img/international.jpg
max-visits: 3

At the Labor and Socialist International, we can forge contacts with our fraternal socialistparties in Europe and across the world.

- @fr_uk: Build relationships with the French SFIO and British Labour Party to move their governments in Germany's favor.
- @austria: Learn organization and street fighting from the Austrian socialists.
- @scandi: Learn the path of economics and reform from the Scandinavian social democrats.
- @comintern: Forget the second international - embed a spy in the Comintern's conference.
- @america: Contact the Socialist party of America.
- @italia: Изучить итальянский опыт. 
- @return: We do not need international contacts at the moment.
- @easy_discard

@fr_uk
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; west_relation += 1; reformist_strength += 5; pacifism += 1; moderate_plan_support += 1

The socialists of France and Britain can influence their governments to be friendlier towards Germany. Hopefully they will reduce our reparations burden.

@austria
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
audio: shuffle music/communist/workers_of_vienna_normalized.mp3
on-arrival: left_strength += 5; radicalization += 1; rb_militancy += 0.03; resources -= 1

Our Reichsbanner can learn much in paramilitary organization and training from the Austrian Republican Protection Units.

@scandi
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: reformist_strength += 8; wtb_support += 1; resources -= 1; moderate_plan_support += 1

The programs of the Nordic socialists involve active government intervention in the economy. We can learn from their economic strategies, and how they moved beyond their working-class base to attract a broader set of constituents.

@comintern
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1; kpd_relation += 5*(1-dissent); communist_coalition += 1 if comintern_seen == 0; comintern_seen += 1

The Stalinists of the KPD insists on calling us "social fascists", suggesting that we social democrats are as bad or worse than actual fascists. Needless to say, this is not good for our relationship with the Communists. By observing the Comintern, we can develop strategies to counter Stalinist rhetoric, at least on a local level.

@america
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1

The Socialist Party of America is tiny and has limited support. There is not much we can do for them, and conversely, not much they can do for us.

@italia
subtitle: -1 resources
choose-if: resources >= 1 and (nsdap_relation > 0 and left_strength < center_strength)
view-if: nsdap_relation > 0
unavailable-subtitle: [? if resources < 1 : Not enough resources. ?][? if nsdap_relation <= 0 : Мы не собираемся связываться с фашистами. ?][? if center_strength < left_strength : Левая часть партии никогда на это не согласится. ?]
on-arrival: resources -= 1; nsdap_relation += 6*(1-dissent); kpd_relation -= 5; z_relation -= 1; dvp_relation -= 3; ddp_relation -= 4; left_dissent += 6; labor_dissent += 1; center_dissent -= 1; dnvp_relation += 3; kpd_relation -= 5

Коммунисты убедились, что они были правы называя нас "социал-фашистами". Итальянские фашисты отказались принимать нашу делегацию. Лишь немногие правые положительно восприняли нашу попытку наладить связи с режимом Муссолини.

@reduce

We do not need the international contacts at the moment.
